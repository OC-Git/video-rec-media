<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
        width="640" height="480" layout="absolute"
        creationComplete="application1_creationCompleteHandler(event)">

	    <mx:Style>
	    	#box {
	    		background-color: #000000;
	    	}
	    	#bandwidth {
	    		color: #ffffff;
	    	}
	    	.big {
				font-size: 120px;
				color: #FFFFFF;
	    	}
	    	.msg {
				font-size: 18px;
				color: #000000;
	    	}
			.button {
				-moz-box-shadow:inset 0px 0px 0px 0px #ffffff;
				-webkit-box-shadow:inset 0px 0px 0px 0px #ffffff;
				box-shadow:inset 0px 0px 0px 0px #ffffff;
				background-color:#ededed;
				-moz-border-radius:6px;
				-webkit-border-radius:6px;
				border-radius:6px;
				border:0px solid #dcdcdc;
				display:inline-block;
				color:#444444;
				font-family:arial;
				font-size:16px;
				padding:6px 24px;
				height: 30px;
				width: 400px;
				text-decoration:none;
				text-shadow:1px 1px 0px #ffffff;
			}
			.button:hover {
				background-color:#dfdfdf;
			}
			.button:active {
				position:relative;
				top:1px;
			}	    	
	    </mx:Style>

        <mx:Script>
                <![CDATA[
	                    import mx.core.UIComponent;
						import mx.core.FlexGlobals;
                        import mx.events.FlexEvent;
						import mx.events.CloseEvent;
                        import mx.controls.Alert;
                        import mx.managers.PopUpManager;
  						import mx.containers.Panel;
                        import flash.external.*;
                        import flash.events.DataEvent;
                        import flash.events.ProgressEvent;
	                    import flash.net.FileReference;
  						
                        private var connection:NetConnection;
                        private var video:Video;
                        private var stream:NetStream;
                        private var camera:Camera;
						private var mic:Microphone;
         				private var t:Timer;
         				                        
						private var param:Object;
						private var recording:Boolean;
						private var seconds:int=0;
						private var secondsMax:int=60;

						private var countdownCanvas:Canvas;
						private var countdown:int;
						private var countdownLabel:Label;
						private var countdownTimer:Timer;
						private var startRecord:Canvas;
						private var stopRecord:Canvas;
						private var stopRecordLabel:Label;
						private var uploadLabel:Label;

						private var file:FileReference;

                        protected function application1_creationCompleteHandler(event:FlexEvent):void
                        {
							param = FlexGlobals.topLevelApplication.parameters;
							recording = false;
							
                            connection = new NetConnection();
                            connection.connect(param.rtmp,
                              param.name, 
                              param.client, param.page, param.title, param.category, 
                              param.description, param.key);
                            connection.addEventListener(NetStatusEvent.NET_STATUS, onConnected);

			                t = new Timer(1000);
			                t.addEventListener(TimerEvent.TIMER, updateTimer);
			                t.start();
                        }
                        
                        private function onConnected(event:NetStatusEvent) : void {
			                switch( event.info.code ) {
			                    case "NetConnection.Connect.Success":
			                        connectionSuccess();
			                    break;
			                    case "NetConnection.Connect.Failed":
			                        connectionFailed("Verbindung nicht erfolgreich");
			                    break;
			                    case "NetConnection.Connect.Rejected":
			                        connectionFailed("Verbindung nicht erlaubt");
			                    break;
			                    default:
			                        trace( "netStatusHandler:code: " + event.info.code );
			                    break;
			                }
						}

						private function connectionSuccess():void {
                            video = new Video(320, 240);
                            
                            // create camera
                            camera = Camera.getCamera();
							camera.setMode(param.width, param.height, param.fps);
                            camera.setQuality(param.bandwidth, param.quality);

							mic = Microphone.getMicrophone();
                            
                            // create the netstream
                            stream = new NetStream(connection);
                        	stream.addEventListener(NetStatusEvent.NET_STATUS, onPublishStream);
                            stream.attachCamera(camera);
							stream.attachAudio(mic);
                            stream.publish(param.name);
                            stream.bufferTime = 60;

                            video.attachCamera(camera);
							                                
                            // wrap video
                            var ui:UIComponent = new UIComponent();
                            ui.addChild(video);
                            ui.move(0,0);
                            vid.addChild(ui);

							startRecord = createCanvas();
							createButton(stopRecord, 30, "Aufnahme starten?", recordClicked);
                        }

                        private function onConsumeStream(event:NetStatusEvent) : void {
                        	if (event.info.code=="NetStream.Play.Stop") {
								PopUpManager.addPopUp(stopRecord,this,true);
								PopUpManager.centerPopUp(stopRecord); 
                        	}
						}

                        private function onPublishStream(event:NetStatusEvent) : void {
			                switch( event.info.code ) {
			                    case "NetStream.Failed":
			                        connectionFailed("Ihr Ticket ist nicht gültig.");
			                    break;
			                    default:
			                        trace( "netStatusHandler:code: " + event.info.code );
			                    break;
			                }
						}


						private function connectionFailed(s:String):void {
							Alert.show(s, "Fehler", Alert.OK);
						}
			
			            private function updateTimer(evt:TimerEvent):void {
			            	var txt:String = "";

			            	if (stream!=null)
			            		txt += String(int(stream.info.currentBytesPerSecond/1024)+" KB/s");
			            		
			                if (recording) {
			                	progress.setProgress(seconds,secondsMax);
			                	seconds++;
				                if (seconds>secondsMax)
				                	recordClicked();
			                }
			                bandwidth.text = txt;
			            }
			            
			            private function createCanvas():Canvas {
							var cvs:Canvas = new Canvas();
							cvs.width = 250;
            				cvs.height = 220;
							cvs.setStyle("backgroundAlpha", 0.8);
							return cvs;
						}			            
			            private function createButton(cvs:Canvas, y:int, label:String, clicked:Function):Button {
							var button:Button = new Button();
							button.label = label;
							button.left = 0;
							button.top = y;
							button.percentWidth = 100;
							button.styleName = "button";
							button.addEventListener(MouseEvent.CLICK, clicked);
							cvs.addChild(button);
							return button;
			            }
			            private function createLabel(cvs:Canvas, x:int, y:int, msg:String):Label {
							var label:Label = new Label();
							label.text = msg;
							label.left = x;
							label.top = y;
							cvs.addChild(label);
							return label
			            }
			            
						private function recordClicked():void {
							if (recording) {
								connection.call("streamManager.stopRecordingShow", null);	
			                	progress.setProgress(0,100);
			                	seconds = 0;
								recordButton.label = "Aufnahme";
								uploadButton.enabled = true;
								stopRecord = createCanvas();
								createButton(stopRecord,  0, "Aufnahme ansehen", previewClicked);
								createButton(stopRecord, 30, "Neue Aufnahme", rerecordClicked);
								createButton(stopRecord, 60, "Aufnahme veröffentlichen", publishClicked);
								createButton(stopRecord, 90, "Video hochladen", doUploadClicked);
								stopRecordLabel = new Label();
								stopRecordLabel.text = "";
								stopRecordLabel.left = 0;
								stopRecordLabel.top = 90;
								stopRecord.addChild(stopRecordLabel);
								PopUpManager.addPopUp(stopRecord,this,true);
								PopUpManager.centerPopUp(stopRecord); 
							} else {
								countdown = 3;
								countdownCanvas = createCanvas();
								countdownLabel = createLabel(countdownCanvas, 80, 0, "3")
								countdownLabel.styleName = "big";
								PopUpManager.addPopUp(countdownCanvas,this,true);
								PopUpManager.centerPopUp(countdownCanvas); 
				                countdownTimer = new Timer(1000);
				                countdownTimer.addEventListener(TimerEvent.TIMER, countdownListener);
				                countdownTimer.start();
							}
							recording = !recording;
						}
						
						private function rerecordClicked(event:Event):void {
    				        connection.call("streamManager.deleteRecording", null);
							PopUpManager.removePopUp(stopRecord);
                            // create the netstream
                            stream = new NetStream(connection);
                        	stream.addEventListener(NetStatusEvent.NET_STATUS, onPublishStream);
                            stream.attachCamera(camera);
							stream.attachAudio(mic);
                            stream.publish(param.name);
                            stream.bufferTime = 60;

                            video.attachCamera(camera);
							recordClicked(); 
						}
						
						private function previewClicked(event:Event):void {
							PopUpManager.removePopUp(stopRecord);
                            stream = new NetStream(connection);
                        	stream.addEventListener(NetStatusEvent.NET_STATUS, onConsumeStream);
                        	stream.play(param.name);
                        	video.attachNetStream(stream);
						}
						
						private function publishClicked(event:Event):void {
  				            var responder:Responder = new Responder(publishComplete, publishFail);
				            connection.call("streamManager.publishRecording", responder);
				            stopRecordLabel.text = "Veröffentlichung läuft, einen Moment bitte...";
							// PopUpManager.removePopUp(stopRecord);
						}
						
			            private function countdownListener(evt:TimerEvent):void {
			            	countdown = countdown-1;
			            	if (countdown==0) {
			            		countdownTimer.stop();
								uploadButton.enabled = false;
								recordButton.label = "Beenden";
								PopUpManager.removePopUp(countdownCanvas); 
								connection.call("streamManager.recordShow", null);
			            	} else {
				            	countdownLabel.text = ""+countdown;
				            }
				        }
						
						private function closeHandler(event:CloseEvent):void
					      {
					        if (event.detail == Alert.YES)
					        {
					          var responder:Responder = new Responder(publishComplete, publishFail);
					          connection.call("streamManager.publishRecording", responder);
					        }
					        else
					        {
					          connection.call("streamManager.deleteRecording", null);
							  ExternalInterface.call("publishCancelled");
					        }
					      }    

						private function publishComplete(results:Object):void {
				            stopRecordLabel.text = "Ihre Aufnahme ist veröffentlicht.";

							if (!ExternalInterface.available)
								Alert.show("Wrapper not available", "Sorry", Alert.OK);
							
							ExternalInterface.call("publishComplete");
				        }
				        
				        private function publishFail(results:Object):void {
							Alert.show("Veröffentlichung gescheitert.", "Sorry", Alert.OK);
				        }
        				public function doUploadClicked(event:Event):void {
							PopUpManager.removePopUp(stopRecord);
							uploadClicked();
						}
						public function uploadClicked():void {
							file = new FileReference();
							file.addEventListener(Event.SELECT, fileSelected);
							file.addEventListener( Event.COMPLETE, fileLoaded );
							file.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA, fileUploaded);
							file.browse([]);
						}
						public function fileProgress(event:ProgressEvent):void {
		                	progress.setProgress(event.bytesLoaded,event.bytesTotal);
		                	if (event.bytesLoaded==event.bytesTotal) {
			                	uploadLabel.text = "Veröffentliche Video...";
							}
						}
						public function fileSelected(event:Event):void {
							ExternalInterface.call( "console.log" , "file selected");

							var cvs:Canvas = createCanvas();
							uploadLabel = createLabel(cvs, 0, 40, "Lade Video hoch...")
							uploadLabel.styleName = "msg";
							PopUpManager.addPopUp(cvs,this,true);
							PopUpManager.centerPopUp(cvs); 
							
							var dest:URLRequest = new URLRequest(param.upload);
							var params:URLVariables = new URLVariables();
							params.title = param.title;
							params.page = param.page;
							params.key = param.key;
							params.category = param.category;
							params.description = param.description;
							
							dest.method = URLRequestMethod.POST;
							dest.data = params;
							file.addEventListener(ProgressEvent.PROGRESS, fileProgress);
							file.upload(dest, "file");
						}
						public function fileLoaded(event:Event):void {
							file.load();				
						}
						public function fileUploaded(event:Event):void {
							file.removeEventListener( ProgressEvent.PROGRESS, fileProgress );
							if (!ExternalInterface.available)
								Alert.show("Wrapper not available", "Sorry", Alert.OK);
							
							ExternalInterface.call("publishComplete");
						}
                ]]>
        </mx:Script>
        <mx:Box x="0" y="0" width="100%" height="100%" verticalGap="0">
	        <mx:Canvas id="vid" x="0" y="0" width="320" height="240"/>
	        <mx:HBox id="box" width="100%" height="26"
		        paddingTop="3" 
		        paddingBottom="3" 
		        paddingLeft="10" 
		        paddingRight="10">
	        	<mx:Button id="recordButton" toggle="true"
	        		label="Aufnahme"
	        		click="recordClicked()"
	        	/>
	        	<mx:Button id="uploadButton"
	        		label="Hochladen"
	        		click="uploadClicked()"
	        	/>
				<mx:ProgressBar width="100" mode="manual" minimum="0" maximum="100" id="progress"/>
		        <mx:Label x="2" y="2" text="" id="bandwidth"/>
		    </mx:HBox> 
	    </mx:Box> 
</mx:Application>
